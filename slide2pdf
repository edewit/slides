#!/usr/bin/env groovy

/**
 * slides2png plays a dzslides presentation using WebDriver, captures each
 * slide to a PNG and generates a shell script to collate the PNGs together
 * into a PDF using convert (from ImageMagick).
 *
 * The URL of the presentation is passed as the sole argument to the script.
 * If the presentation is local, specify the absolute path prefixed with the
 * file:// protocol.
 *
 * @author Dan Allen
 * @license ASLv2
 */

@Grapes([
    @Grab("org.gebish:geb-core:0.9.0-RC-1"),
    @Grab("org.seleniumhq.selenium:selenium-chrome-driver:2.26.0")
])
import geb.Browser

if (args.length == 0) {
  println "Please specify the URL of the presentation"
  return
}

def url = args[0]

def reportsDir = '/tmp/geb-reports'
if (args.length > 1) {
  reportsDir = args[1]
}

Browser.drive {
    config.reportsDir = new File(reportsDir)
    cleanReportGroupDir()
    go url
 
    def idx = 1
    def body = $('body')
    //body << 'f'
    sleep 500
    def script = null
    def done = false;
    while (true) {
      report "slide_" + idx
      if (script == null) {
        script = new File(getReportGroupDir(), 'to_pdf.sh')
        script.append("#!/bin/sh\n\ncd `dirname \$0`\nconvert")
      }
      script.append(' slide_' + idx + '.png')
      body << ' '
      idx++
      if (done) {
        break;
      }
      done = !$('slide.thank-you-slide.current').isEmpty()
      sleep 1500
    }
    script.append(' presentation.pdf')
    script.executable = true
    [script.absolutePath].execute().waitFor()
}.quit()